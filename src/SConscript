import os.path

# import these variables from the parent build script
Import('env', 'exe_env', 'lib_sources', 'target_name', 'target_dir',
       'example_sources',
       'test_sources', 'test_libs')

env.Library(target=target_name, source=lib_sources)

# Unit test runner
if test_sources:
	tst = exe_env.Clone()
	tst.Append(LIBS=test_libs)
	
	test_prg = tst.Program(target="test", source=test_sources)
	test_alias = tst.Alias('test', test_prg, test_prg[0].path)
	tst.AlwaysBuild(test_alias)

if example_sources:
	for example_source in example_sources:
		exm = exe_env.Clone()
		exm_target = os.path.basename(example_source)
		exm_target = os.path.splitext(exm_target)[0]
		exm_prg = exm.Program(target=exm_target, source=example_source)
		exm_alias = exm.Alias('example', exm_prg)
